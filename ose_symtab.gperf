%{
/*
Copyright (c) 2019-20 John MacCallum
Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/
#include <stdlib.h>
#include <string.h>
#include "ose.h"
#include "ose_stackops.h"
#include "ose_builtins.h"
%}
%readonly-tables
%global-table
%struct-type
%define hash-function-name _ose_symtab_hash
%define lookup-function-name _ose_symtab_lookup
%define word-array-name _ose_symtab_wordlist
struct _ose_symtab_rec { char *name; void (*f)(ose_bundle);};
%%
################################################################################
### Stack Operations
################################################################################
/2drop, ose_builtin_2drop
/2dup, ose_builtin_2dup
/2over, ose_builtin_2over
/2swap, ose_builtin_2swap
/drop, ose_builtin_drop
/dup, ose_builtin_dup
/nip, ose_builtin_nip
/-rot, ose_builtin_notrot
/over, ose_builtin_over
/pick/jth, ose_builtin_pick
/pick/bottom, ose_builtin_pickBottom
/pick/match, ose_builtin_pickMatch
/roll/jth, ose_builtin_roll
/roll/bottom, ose_builtin_rollBottom
/roll/match, ose_builtin_rollMatch
/rot, ose_builtin_rot
/swap, ose_builtin_swap
/tuck, ose_builtin_tuck
################################################################################
### Grouping / Ungrouping
################################################################################
/bundle/all, ose_builtin_bundleAll
/bundle/frombottom, ose_builtin_bundleFromBottom
/bundle/fromtop, ose_builtin_bundleFromTop
/clear, ose_builtin_clear
/clear/payload, ose_builtin_clearPayload
/pop, ose_builtin_pop
/pop/all, ose_builtin_popAll
/pop/all/drop, ose_builtin_popAllDrop
/pop/all/bundle, ose_builtin_popAllBundle
/pop/all/drop/bundle, ose_builtin_popAllDropBundle
/push, ose_builtin_push
/unpack, ose_builtin_unpack
/unpack/drop, ose_builtin_unpackDrop
/unpack/bundle, ose_builtin_unpackBundle
/unpack/drop/bundle, ose_builtin_unpackDropBundle
################################################################################
### Queries
################################################################################
/count/elems, ose_builtin_countElems
/count/items, ose_builtin_countItems
/length/address, ose_builtin_lengthAddress
/length/tt, ose_builtin_lengthTT
/length/item, ose_builtin_lengthItem
/lengths/items, ose_builtin_lengthsItems
/size/address, ose_builtin_sizeAddress
/size/elem, ose_builtin_sizeElem
/size/item, ose_builtin_sizeItem
/size/payload, ose_builtin_sizePayload
/sizes/elems, ose_builtin_sizesElems
/sizes/items, ose_builtin_sizesItems
/size/tt, ose_builtin_sizeTT
################################################################################
### Operations on Bundle Elements and Items
################################################################################
/blob/toelem, ose_builtin_blobToElem
/blob/totype, ose_builtin_blobToType
/concat/blobs, ose_builtin_concatenateBlobs
/concat/strings, ose_builtin_concatenateStrings
/address, ose_builtin_copyAddressToString
/payload, ose_builtin_copyPayloadToBlob
/string/toaddress/swap, ose_builtin_swapStringToAddress
/tt, ose_builtin_copyTTToBlob
/decat/blob, ose_builtin_decatenateBlob
/decat/string, ose_builtin_decatenateString
/elem/toblob, ose_builtin_elemToBlob
/item/toblob, ose_builtin_itemToBlob
/join/blobs, ose_builtin_joinBlobs
/join/strings, ose_builtin_joinStrings
/string/toaddress/move, ose_builtin_moveStringToAddress
/split/blob/fromend, ose_builtin_splitBlobFromEnd
/split/blob/fromstart, ose_builtin_splitBlobFromStart
/split/string/fromend, ose_builtin_splitStringFromEnd
/split/string/fromstart, ose_builtin_splitStringFromStart
/swap/bytes/4, ose_builtin_swap4Bytes
/swap/bytes/8, ose_builtin_swap8Bytes
/swap/bytes/n, ose_builtin_swapNBytes
/trim/blob, ose_builtin_trimBlob
/trim/string, ose_builtin_trimString
/match, ose_builtin_match
/pmatch, ose_builtin_pmatch
################################################################################
### Creatio Ex Nihilo
################################################################################
/make/bundle, ose_builtin_pushBundle
/push/blob, ose_builtin_makeBlob
################################################################################
### Arithmetic
################################################################################
/add, ose_builtin_add
/sub, ose_builtin_sub
/mul, ose_builtin_mul
/div, ose_builtin_div
/mod, ose_builtin_mod
/neg, ose_builtin_neg
/eql, ose_builtin_eql
/lte, ose_builtin_lte
/lt, ose_builtin_lt
/and, ose_builtin_and
/or, ose_builtin_or
################################################################################
### Predicates
################################################################################
/is/addresschar, ose_builtin_isAddressChar
/is/type/known, ose_builtin_isKnownTypetag
/is/type/string, ose_builtin_isStringType
/is/type/int, ose_builtin_isIntegerType
/is/type/float, ose_builtin_isFloatType
/is/type/numeric, ose_builtin_isNumericType
/is/type/unit, ose_builtin_isUnitType
/is/type/bool, ose_builtin_isBoolType
################################################################################
### Builtins
################################################################################
/eval, ose_builtin_eval
/if, ose_builtin_if
/else, ose_builtin_else
/end/if, ose_builtin_end_if
/dotimes, ose_builtin_dotimes
/end/dotimes, ose_builtin_end_dotimes
/move/elem/todest, ose_builtin_moveElemToDest
/copy/elem/todest, ose_builtin_copyElemToDest
/replace/elem/indest, ose_builtin_replaceElemInDest
/quote, ose_builtin_quote
/end/quote, ose_builtin_end_quote
/clear/env, ose_builtin_clearEnv
/copy/env, ose_builtin_copyEnv
###
%%

void (*ose_symtab_lookup(char *str))(ose_bundle)
{
	const struct _ose_symtab_rec *r = _ose_symtab_lookup(str, strlen(str));
	if(r){
		return r->f;
	}else{
		return NULL;
	}
}

int ose_symtab_len(void)
{
	return TOTAL_KEYWORDS;
}

char *ose_symtab_getNthSym(int n)
{
	int c = 0, i = 0;
	//for(int i = MIN_HASH_VALUE; i < MAX_HASH_VALUE; i++){
	while(c < TOTAL_KEYWORDS){
		char *name = _ose_symtab_wordlist[i++].name;
		if(strlen(name) >= MIN_WORD_LENGTH){
			if(c == n){
				return name;
			}
			c++;
		}
	}
	return NULL;
}